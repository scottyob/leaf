
// includes
  #include <Arduino.h>
  #include <HardwareSerial.h>
  #include "buttons.h"
  #include "power.h"
  #include "Leaf_SPI.h"
  #include "SDcard.h"
  #include "display.h"
  #include "gps.h"
  #include "baro.h"
  #include "IMU.h"
  #include "speaker.h"
  #include "log.h"
  #include "settings.h"


// Pinout for ESP32 Leaf V2
  #define AVAIL_GPIO_0       0  // unused, broken out to header
  #define AVAIL_GPIO_21     21  // unused, broken out to header (also used as LCD backlight if desired)
  #define AVAIL_GPIO_39     39  // unused, broken out to header

// Main system event/task timer
  hw_timer_t *task_timer = NULL;
  #define TASK_TIMER_FREQ 1000  // run at 1000Hz 
  #define TASK_TIMER_LENGTH 10  // trigger the ISR every 10ms
  void onTaskTimer(void);

// Standby system timer (when on USB power and charging battery, but otherwise "off" (i.e., soft off))
  hw_timer_t *charge_timer = NULL;
  #define CHARGE_TIMER_FREQ 1000    // run at 40Hz 
  #define CHARGE_TIMER_LENGTH 500  // trigger the ISR every 500ms
  void onChargeTimer(void);
  char chargeman_doTasks = 0;     // let the ISR timer set this to 1 so we're on proper timing cycle each time
  uint64_t sleepTimeStamp = 0;    // track micros() for how long we should sleep in charge mode between actions.

  // Counters for system task timer
  char counter_10ms_block = 0;
  char counter_100ms_block = 0;

// flag if the GPS serial port is done sending data for awhile
  bool gps_is_quiet = 0;

// Trackers for Task Manager Queue.  Default to tasks being needed, so they execute upon startup
  char taskman_setTasks = 1;  // the task of setting tasks -- usually set by timer-driven interrupt

  char taskman_buttons = 1;   // poll & process buttons
  char taskman_baro = 1;      // (1) preprocess on-chip ADC pressure, (2) read pressure and preprocess on-chip ADC temperature, (3) read temp and calulate true Alt, (4) filter Alt, update climb, and store values etc
  char taskman_imu = 1;       // read sensors and update values
  char taskman_gps = 1;       // process any NMEA strings and update values
  char taskman_display = 1;   // update display
  char taskman_power = 1;     // check battery, check auto-turn-off, etc
  char taskman_log = 1;       // check auto-start, increment timers, update log file, etc

// track current power on state to detect changes (if user turns device on or off while USB is plugged in, device can still run even when "off")
  uint8_t currentPowerOnState = POWER_OFF;

// temp testing stuff
  //uint8_t display_page = 0;
  uint8_t display_do_tracker = 1;
  #define TESTING_LOOP false




/////////////////////////////////////////////////
//             SETUP              ///////////////
/////////////////////////////////////////////////
void setup() {

  // Start USB Serial Debugging Port
    Serial.begin(115200);
    delay(1000);
    Serial.println("Starting Setup");

  // turn on and handle all device initialization
    power_bootUp(); 

  //Start Main System Timer for Interrupt Events (this will tell Main Loop to set tasks every interrupt cycle)
    task_timer = timerBegin(TASK_TIMER_FREQ);            
    timerAttachInterrupt(task_timer, &onTaskTimer); // timer, ISR call          NOTE: timerDetachInterrupt() does the opposite
    timerAlarm(task_timer, TASK_TIMER_LENGTH, true, 0);      // auto reload timer ever time we've counted a sample length

  //Start Charge System Timer for Interrupt Events (this will tell Main Loop to do tasks every interrupt cycle)
    charge_timer = timerBegin(CHARGE_TIMER_FREQ);            
    timerAttachInterrupt(charge_timer, &onChargeTimer); // timer, ISR call          NOTE: timerDetachInterrupt() does the opposite
    timerAlarm(charge_timer, CHARGE_TIMER_LENGTH, true, 0);      // auto reload timer ever time we've counted a sample length

  // All done!
    Serial.println("Finished Setup");

    //Serial.println("Testing SD Card");
    //SDcard_test();
}


// Main Timer Interrupt
void IRAM_ATTR onTaskTimer() {
  //do stuff every alarm cycle (default 10ms)
  taskman_setTasks = 1; // next time through main loop, set tasks to do!
  // wakeup();  // go back to main loop and keep processing stuff that needs doing!
  spi_checkFlag(1);
}


// Charge Timer Interrupt
void IRAM_ATTR onChargeTimer() {
  sleepTimeStamp = micros();
  //do stuff every alarm cycle (default 10ms)
  chargeman_doTasks = 1; // next time through main loop, set tasks to do!
  // wakeup();  // go back to main loop and keep processing stuff that needs doing!
}


/////////////////////////////////////////////////
// Main Loop for Processing Tasks ///////////////
/////////////////////////////////////////////////
/*
The main loop perioritizes processesing any data in the serial buffer so as not to miss any NMEA GPS characters.  
When the serial buffer is empty, the main loop will move on to processing any other remaining tasks.

The gps serial port is considered 'quiet' if we received the last character of the last NMEA sentence.  We shouldn't expect more sentences for awhile (~1 second GPS updates)
If the serial port is quiet, and all tasks are done, then the processor will go to sleep for the remainder of the 10ms time block.
Every 10ms, driven by an interrupt timer, the system will wake up, set flags for which tasks are needed, and then return to running the main loop.

TODO: In addition to the timer-driven interrupt, we may consider also setting wake-up interrupts for the pushbuttons, the GPS 1PPS signal, and perhaps others.
*/

// LOOP NOTES:
// when re-entering POWER_ON state, be sure to start from tasks #1, so baro ADC can be re-prepped before reading

void loop() {
  if (TESTING_LOOP) main_loop_test();
  else if (powerOnState == POWER_ON) main_ON_loop();
  else if (powerOnState == POWER_OFF_USB) main_CHARGE_loop();
  else Serial.print("FAILED MAIN LOOP HANDLER");
}

bool goToSleep = false;
uint64_t taskTimeLast = 0;
uint64_t taskTimeNow = 0;
uint32_t taskDuration = 0;

void main_CHARGE_loop() {
  if (chargeman_doTasks) {    
    taskTimeNow = micros();
    taskDuration = taskTimeNow - taskTimeLast;
    taskTimeLast = taskTimeNow;
    Serial.println(" ");
    Serial.print("taskDuration: "); Serial.println(taskDuration);

    display_setPage(page_charging);
    display_update();       // update display based on battery charge state etc
    uint8_t buttonPushed = buttons_update();       // check buttons for any presses (user can turn ON from charging state)
    chargeman_doTasks = 0;  // done with tasks this timer cycle
    if (buttonPushed == NONE) goToSleep = true; // get ready to sleep if no button is being pushed
  } else {    
    if (goToSleep && ECO_MODE) {  // don't allow sleep if ECO_MODE is off
      // we don't want to sleep again as soon as we wake up; we want to wait until we've done 'doTasks' before sleeping again
      goToSleep = false;

      // Wake up if button pushes
      esp_sleep_enable_ext0_wakeup((gpio_num_t)BUTTON_PIN_CENTER, HIGH);
      esp_sleep_enable_ext0_wakeup((gpio_num_t)BUTTON_PIN_LEFT, HIGH);    // TODO: we probably only need to wake up with center button
      esp_sleep_enable_ext0_wakeup((gpio_num_t)BUTTON_PIN_RIGHT, HIGH);
      esp_sleep_enable_ext0_wakeup((gpio_num_t)BUTTON_PIN_UP, HIGH);
      esp_sleep_enable_ext0_wakeup((gpio_num_t)BUTTON_PIN_DOWN, HIGH);

      // or wake up with timer
      uint64_t microsNow = micros();
      uint64_t sleepMicros = sleepTimeStamp + (1000 * (CHARGE_TIMER_LENGTH - 1)) - microsNow;  // sleep until just 1ms before the expected next cycle of CHARGE_TIMER
      //Serial.print("microsNow:    "); Serial.println(microsNow);
      //Serial.print("sleepMicros:  "); Serial.println(sleepMicros);
      //Serial.print("wakeMicros:   "); Serial.println(microsNow + sleepMicros);
      
      if(sleepMicros > 496*1000) sleepMicros = 496*1000;  // ensure we don't go to sleep for too long if there's some math issue (or micros rollover, etc).            
      esp_sleep_enable_timer_wakeup(sleepMicros);         // set timer to wake up
      //delayMicroseconds(sleepMicros);                   // use delay instead of actual sleep to test sleep logic (allows us to serial print during 'fake sleep' and also re-program over USB mid-'fake sleep')
      esp_light_sleep_start();        
    } else {
      if (buttons_update() != NONE) Serial.println("we see a button!");
    }
  }
}

//uint32_t millisBegin;
void main_ON_loop() {    
  if (taskman_setTasks) {   // check flag set by timer interrupt.  We only do this once every 10ms
    //millisBegin = millis();   // save the timestamp when we enter this 10ms block... we'll need to be done within 10ms!
    //Serial.println(millisBegin);
    setTasks();             // set necessary tasks for this 10ms block
    taskman_setTasks = 0;   // we don't need to do this again until the next 10ms timer interrupt
  }

  // do necessary tasks
  taskManager(); 
 
  // GPS Serial Buffer Read
    // stop reading if buffer returns empty, OR, if our 10ms time block is up (because main timer interrupt fired and set setTasks to true)
    bool gps_buffer_full = true;
    while (gps_buffer_full && !taskman_setTasks) {   
      gps_buffer_full = gps_read_buffer_once();
    }

  //if (gps_is_quiet) goToSleep();                 
  //else, run this loop again and keep processing the serial buffer until we're done with all the NMEA sentences this cycle

  //TODO: if gps serial buffer can fill while processor is sleeping, then we don't need to wait for serial port to be quiet
}


void setTasks(void) {    
  // increment time counters
  if(++counter_10ms_block >= 10) {
    counter_10ms_block = 0;           // every 10 periods of 10ms, go back to 0 (100ms total)
    if (++counter_100ms_block >= 10) {
      counter_100ms_block = 0;        // every 10 periods of 100ms, go back to 0 (1sec total)      
    }
  }

  // tasks to complete every 10ms
  taskman_buttons = 1;

  // set additional tasks to complete, broken down into 10ms block cycles.  (embedded if() statements allow for tasks every second, spaced out on different 100ms blocks)
  switch (counter_10ms_block) {
    case 0:
      taskman_baro = 1;  // begin updating baro every 50ms on the 0th and 5th blocks
      break;
    case 1:
      taskman_baro = 2;
      break;
    case 2:
      taskman_baro = 3;
      break;
    case 3:
      taskman_baro = 4;
      break;
    case 4:
      taskman_imu = 1;  // update accel every 100ms during the 4th block
      break;
    case 5:
      taskman_baro = 1;  // begin updating baro every 50ms on the 0th and 5th blocks
      break;
    case 6:
      taskman_baro = 2;
      break;
    case 7:
      taskman_baro = 3;
      break;
    case 8:
      taskman_baro = 4;
      break;
    case 9:
      
      // Tasks every second complete here in the 9th 10ms block.  Pick a unique 100ms block for each task to space things out

      // gps update every half second (this avoid any aliasing issues if we keep trying to update GPS in the middle of )
      if (counter_100ms_block == 0 || counter_100ms_block == 5) taskman_gps = 1;         
      if (counter_100ms_block == 1) taskman_power = 1;                        // every second: power checks      
      if (counter_100ms_block == 2) taskman_log = 1;                          // every second: logging
      //if (flightTimer_getTime() == 10 && counter_100ms_block == 4) power_sleep_peripherals(); // testing GPS shutdown command

      // Update LCD every half-second on the 3rd and 8th 100ms blocks
      if (counter_100ms_block == 3 || counter_100ms_block == 8) taskman_display = 1;  // every half-second: LCD update
      break;      
  }
}



// execute necessary tasks while we're awake and have things to do
void taskManager(void) {    
  if (taskman_buttons) { buttons_update(); taskman_buttons = 0; }
  if (taskman_baro)    { baro_update(taskman_baro, counter_100ms_block); taskman_baro = 0; }    // update baro, using the appropriate step number
  if (taskman_imu)     { imu_update();     taskman_imu = 0; }
  if (taskman_gps)     { gps_update();     taskman_gps = 0; }
  if (taskman_power)   { power_update();   taskman_power = 0; }
  if (taskman_log)     { log_update();     taskman_log = 0; }
  if (taskman_display) { display_update(); taskman_display = 0; }  
}


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

void main_loop_test() {

  uint8_t button = buttons_check();
  uint8_t button_state = buttons_get_state();


/*
  if (button_state == PRESSED) {
    if (button == LEFT || button == UP) {      
      if (display_page > 0) display_page--;
    } else if (button == RIGHT || button == CENTER || button == DOWN) {
      display_page++;
      if (display_page > 12) display_page = 12;
    }
    display_do_tracker = 1;
    Serial.print("Going to page: ");
    Serial.println(display_page);
  }

  switch (display_page) {
    case 0:
      //full_system_test();
      //gps_test_sats();
      gps_test();
      break;
    case 1:
  
      //display_test_bat_icon();      
      //display_test();      
      //power_test();
      if (display_do_tracker) {
        full_system_test();
        //display_test_real_3();
        //SDcard_test();
        //display_do_tracker = 0;
      }
      break;
    case 2:
      speaker_TEST();
      break;
    case 3:      
      //baro_test();
      break;
    case 4:
      imu_test();
      break;
    case 5:      
      display_test_big(1);
      break;
    case 6:
      display_test_big(2);      
      break;
    case 7:
      display_test_big(3);      
      break;
    case 8:
      display_test_big(4);      
      break;
    case 9:
      display_test_big(5);      
      break;
    case 12:
      speaker_playSound(fx_exit);
      delay(2000);
      power_turn_off();
      break;
  }
  */
}

void full_system_test() {
  delay(500);
  // update baro sensor
  taskman_baro = baro_update(taskman_baro, 7);
  if (taskman_baro == 0) taskman_baro = 1;
  
  //for (int i=0; i<300000; i++) {
    speaker_updateVarioNote(baro_getClimbRate());
  //}
  speaker_debugPrint();

  // update display
  display_page_thermal();

  // allow setting volume by sending a 0, 1, 2, 3 character over Serial  
  if (Serial.available() > 0) {
    char letter = char(Serial.read());
    while (Serial.available() > 0) {
      Serial.read();
    }
    switch (letter) {
      case '0': speaker_setVolume(0); break;      
      case '1': speaker_setVolume(1); break;      
      case '2': speaker_setVolume(2); break;      
      case '3': speaker_setVolume(3); break;            
    }    
  }
}
