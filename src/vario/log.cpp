
#include <String>

#include "log.h"
#include "baro.h"
#include "gps.h"
#include "IMU.h"
#include "speaker.h"
#include "settings.h"
#include "SDcard.h"

// Flight Timer
  uint32_t flightTimerSec = 0;
  bool flightTimerRunning = 0;
  bool flightTimerResetting = 0;
  char flightTimerString[] = "0:00:00";    // H:MM:SS -> 7 characters max for string.  If it hits 10 hours, will re-format to _HH:MM_
  char flightTimerStringShort[] = "00:00"; // MM:SS -> 5 characters max for string.  If it hits 1 hour, will change to HH:MM

// values for logbook entries
  bool logFlightTrackStarted = false;

  int32_t log_alt = 0;
  int32_t log_alt_start = 0;
  int32_t log_alt_end = 0;
  int32_t log_alt_max = 0;
  int32_t log_alt_min = 0;
  int32_t log_alt_above_launch = 0;
  int32_t log_alt_above_launch_max = 0;
  //TODO: add max and min speed

  int16_t log_climb = 0;
  int16_t log_climb_max = 0;
  int16_t log_climb_min = 0;

  int32_t log_temp = 0;
  int32_t log_temp_max = 0;
  int32_t log_temp_min = 0;


void log_init() {

}


// update function to run every second
void log_update() {

  // everything log-related if the timer IS RUNNING
  if (flightTimerRunning) {
    flightTimerSec += 1;  

    // start the Track if needed (we check every update, in case we didn't have a GPS fix.  This way we can start track log writing as soon as we DO get a fix)
    if (!logFlightTrackStarted) {
      if (gps.location.isValid()) {                
        logFlightTrackStarted = SDcard_createLogFile(); //flag that we've started a log if we actually have successfully started a log file on SDCard.  If this returns false, we can keep trying to save a log file until it's successful.
      }
    }

    // save datapoints to the track
    if (logFlightTrackStarted) {
      SDcard_writeLogData(log_getKMLCoordinates());
    }

    // capture any records for this flight
    log_checkMinMaxValues();  

    // finally, check if we should auto-stop the timer because we've been sitting idle for long enough
    if (AUTO_START && flightTimer_autoStop()) {  
      flightTimer_stop();
    } 

  } else { // if timer NOT running, check if we should auto-start it
    if (AUTO_START && flightTimer_autoStart()) {
      flightTimer_start();
    }
  }
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Auto Start-Stop check functions.

  uint8_t autoStartCounter = 0;

  bool flightTimer_autoStart() {
    bool startTheTimer = false;  // default to not auto-start

    // we will auto-start if EITHER the GPS speed or the Altitude change triggers the starting thresholds.

    // keep track of how many times (seconds) we've been continuously over the min speed threshold
    if (gps.speed.mph() > AUTO_START_MIN_SPEED) {      
      autoStartCounter++;
      if (autoStartCounter >= AUTO_START_MIN_SEC) {
        startTheTimer = true;
        Serial.println("****************************** autoStart TRUE via speed");
      }
    } else {
      autoStartCounter = 0;
    }

    // check if current altitude has changed enough from startup to trigger timer start
    int32_t altDifference = baro_getAlt() - baro_getAltInitial();
    if (altDifference < 0) altDifference *= -1;
    if (altDifference > AUTO_START_MIN_ALT) {
      startTheTimer = true;
      Serial.println("****************************** autoStart TRUE via alt");
    }

  Serial.print("S T A R T   Counter: ");
  Serial.print(autoStartCounter);
  Serial.print("   Alt Diff: ");
  Serial.print(altDifference);
  Serial.print("  StartTheTimer? : ");
  Serial.println(startTheTimer);

    return startTheTimer;
  }

  uint8_t autoStopCounter = 0;
  int32_t autoStopAltitude = 0;

  bool flightTimer_autoStop() {
    bool stopTheTimer = false;  // default to not auto-stop

    // we will auto-stop only if BOTH the GPS speed AND the Altitude change trigger the stopping thresholds.

    // First check if altitude is stable
    int32_t altDifference = baro_getAlt() - autoStopAltitude;    
    if (altDifference < 0) altDifference *= -1;
    if (altDifference < AUTO_STOP_MAX_ALT) {

      // then check if GPS speed is slow enough
      if (gps.speed.mph() < AUTO_STOP_MAX_SPEED) {
        autoStopCounter++;
        if (autoStopCounter >= AUTO_STOP_MIN_SEC) {
          stopTheTimer = true;
        }
      } else {
        autoStopCounter = 0;
      }

    } else {
      autoStopAltitude += altDifference;  //reset the comparison altitude to present altitude, since it's still changing
    }

  Serial.print(" ** STOP ** Counter: ");
  Serial.print(autoStopCounter);
  Serial.print("   Alt Diff: ");
  Serial.print(altDifference);
  Serial.print("  stopTheTimer? : ");
  Serial.println(stopTheTimer);

    return stopTheTimer;
  }



//////////////////////////////////////////////////////////////////////////////////
// FLight Timer Management Functions

  bool flightTimer_isRunning() {
    return flightTimerRunning;
  }

  void flightTimer_start() {
    speaker_playSound(fx_enter);
    flightTimerRunning = 1;

    //starting values
    baro_resetLaunchAlt();
    log_alt_start = baro_getAltAtLaunch();  
  }

  void flightTimer_stop() {  
    // play stopping sound
    if (!flightTimerResetting) speaker_playSound(fx_cancel);    // only play sound if not in the process of resetting (the resetting sound will play from the reset function)

    // finish up log file if timer was running
    if (flightTimerRunning) {

      //ending values
      log_alt_end = baro_getAlt();    
      // TODO: save other min/max values in a csv or similar log file.  Also description of KML file maybe?

      // if a KML track log was started
      if (logFlightTrackStarted) {
        logFlightTrackStarted = false;
        SDcard_writeLogFooter();
        //TODO: other KML file additions; maybe adding description and min/max and other stuff
      }
    }
    flightTimerRunning = 0;
  }

  void flightTimer_toggle() {  
    if (flightTimerRunning) flightTimer_stop();
    else flightTimer_start();
  }

  void flightTimer_reset() {
    flightTimerResetting = true;
    if (flightTimerRunning) flightTimer_stop();
    speaker_playSound(fx_exit);
    flightTimerSec = 0;  
    flightTimerResetting = false;
  }



uint32_t flightTimer_getTime() {
  return flightTimerSec;
}

char * flightTimer_getString(bool shortString) {
  flightTimer_updateStrings();
  if (shortString) return flightTimerStringShort;
  else return flightTimerString;
}

void flightTimer_updateStrings() {
  // char * flightTimerString = "0:00:00";    // H:MM:SS -> 7 characters max for string.  If it hits 10 hours, will re-format to _HH:MM_
  // char * flightTimerStringShort = "00:00"; // MM:SS -> 5 characters max for string.  If it hits 1 hour, will change to HH:MM

  uint8_t hours = flightTimerSec / 3600;
  uint8_t mins  = (flightTimerSec / 60) % 60;
  uint8_t secs  = flightTimerSec % 60;

  uint8_t position = 0;

  // update flightTimerString    
  if (hours > 9) {
    if (hours > 99) 
    //      "000:00 "  HHH:MM_
      flightTimerString[position] = '0' + hours/100;
    else            
    //      " 00:00 "  _HH:MM_
      flightTimerString[position] = ' ';
      flightTimerString[++position] = '0' + hours/10;
      flightTimerString[++position] = '0' + hours % 10;
      flightTimerString[++position] = ':';
      flightTimerString[++position] = '0' + mins/10;
      flightTimerString[++position] = '0' + mins % 10;
      flightTimerString[++position] = ' ';
  } else if (hours > 0) {
    //      "0:00:00"  H:MM:SS
      flightTimerString[position] = '0' + hours;
      flightTimerString[++position] = ':';
      flightTimerString[++position] = '0' + mins/10;
      flightTimerString[++position] = '0' + mins % 10;
      flightTimerString[++position] = ':';
      flightTimerString[++position] = '0' + secs/10;
      flightTimerString[++position] = '0' + secs % 10;
  } else {
      flightTimerString[position++] = ' ';
    if (mins > 9)
    //      " 00:00 "  _MM:SS_
      flightTimerString[position] = '0' + mins/10;
    else 
    //      "  0:00 "  __M:SS_
      flightTimerString[position] = ' ';
      
      flightTimerString[++position] = '0' + mins % 10;
      flightTimerString[++position] = ':';
      flightTimerString[++position] = '0' + secs/10;
      flightTimerString[++position] = '0' + secs % 10;
      flightTimerString[++position] = ' ';
  }

    position = 0;

    // update flightTimerStringShort
      // char * flightTimerStringShort = "00:00"; // MM:SS -> 5 characters max for string.  If it hits 1 hour, will change to HH:MM
    if (hours > 0) {
      if (hours > 9) flightTimerStringShort[position] = '0' + hours/10;
      else flightTimerStringShort[position] = ' ';
      flightTimerStringShort[++position] = '0' + hours % 10;
      flightTimerStringShort[++position] = ':';
      flightTimerStringShort[++position] = '0' + mins/10;
      flightTimerStringShort[++position] = '0' + mins % 10;
    } else {
      if (mins > 9) flightTimerStringShort[position] = '0' + mins/10;
      else flightTimerStringShort[position] = ' ';
      flightTimerStringShort[++position] = '0' + mins % 10;
      flightTimerStringShort[++position] = ':';
      flightTimerStringShort[++position] = '0' + secs/10;
      flightTimerStringShort[++position] = '0' + secs % 10;
    }
}

void log_checkMinMaxValues() {
  //check altitude values for log
  log_alt = baro_getAlt();
  log_alt_above_launch = baro_getAltAboveLaunch();
  if (log_alt > log_alt_max) {
    log_alt_max = log_alt;
    if (log_alt_above_launch > log_alt_above_launch_max) log_alt_above_launch_max = log_alt_above_launch; // we only need to check for max above-launch values if we're also setting a new altitude max.
  } else if (log_alt < log_alt_min) {
    log_alt_min = log_alt;
  } 

  //check climb values for log
  log_climb = baro_getClimbRate();
  if (log_climb > log_climb_max) {
    log_climb_max = log_climb;
  } else if (log_climb < log_climb_min) {
    log_climb_min = log_climb;
  } 

  // check temperature values
  log_temp = baro_getTemp();
  if (log_temp > log_temp_max) {
    log_temp_max = log_temp;
  } else if (log_temp < log_temp_min) {
    log_temp_min = log_temp;
  } 
}


String log_getKMLCoordinates() {
  String lonPoint = String(gps.location.lng(), 7);
  String latPoint = String(gps.location.lat(), 7);  
  String altPoint = String(gps.altitude.meters(), 2);
  String logPointStr = lonPoint + "," + latPoint + "," + altPoint + "\n";
  return logPointStr;
}

String log_createFileName() {
  
  String fileTitle = "FlightTrack";
  String fileDate = String(gps_getLocalDate());
  int32_t timeInMinutes = (gps.time.hour()*60 + gps.time.minute() + 24*60 + TIME_ZONE) % (24*60);
  uint8_t timeHours = timeInMinutes/60;
  uint8_t timeMinutes = timeInMinutes % 60;
  String fileTime = String(timeHours/10) + String(timeHours % 10) + String(timeMinutes / 10) + String(timeMinutes % 10);
  
  Serial.println(timeInMinutes);
  Serial.println(timeHours);
  Serial.println(timeMinutes);
  Serial.println(timeMinutes/10);
  Serial.println(timeMinutes%10);

  String fileName = fileTitle + "_" + fileDate + "_" + fileTime + ".kml";
  Serial.println(fileName);

  return fileName;
}